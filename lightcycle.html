<!DOCTYPE html>
<html>
<head>
<meta charset="utf8">

<script id="vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;
attribute vec4 vNormal;
attribute vec2 vTexCoord;

uniform mat4 vModel;
uniform mat4 vView;
uniform mat4 vProjection;
uniform bool useTexture;
uniform vec4 lightPosition;
uniform bool useLighting;

varying vec2 fTexCoord;
varying vec3 N, V, L, H;

void main()
{
    gl_Position = vProjection * vView * vModel * vPosition;

    if (useLighting) {
        // Convert base coordinates into world space
        vec3 worldPosition = (vView * vModel * vPosition).xyz;
        vec3 worldLightPosition = (vView * lightPosition).xyz;
        vec3 worldNormal = mat3(vView * vModel) * vNormal.xyz;

        // Compute lighting vectors in reflection model
        N = normalize(worldNormal);
        L = normalize(worldLightPosition - worldPosition);
        V = -normalize(worldPosition);
        H = normalize(L + V);
    }

    if (useTexture)
        fTexCoord = vTexCoord;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision mediump float;

varying vec2 fTexCoord;
varying vec3 N, V, L, H;

uniform sampler2D texture;
uniform bool useTexture;
uniform vec4 ambient, diffuse, specular;
uniform float shininess;
uniform bool useLighting;

void main()
{
    vec4 color = vec4(1.0, 1.0, 1.0, 1.0);

    if (useLighting) {
        float Kd = max(dot(L, N), 0.0);
        float Ks = pow(max(dot(N, H), 0.0), shininess);
        if (dot(L, N) < 0.0)
            Ks = 0.0;
        color = ambient + Kd*diffuse + Ks*specular;
    }

    if (useTexture) {
        color *= texture2D(texture, fTexCoord);
    }

    color.a = 1.0;
    gl_FragColor = color;
}
</script>

<script type="text/javascript" src="Common/webgl-utils.js"></script>
<script type="text/javascript" src="Common/initShaders.js"></script>
<script type="text/javascript" src="Common/MV.js"></script>
<script type="text/javascript" src="MV2.js"></script>
<script type="text/javascript" src="lightcycle.js"></script>
<link rel="stylesheet" type="text/css" href="styles.css">
</head>

<body>
<canvas id="gl-canvas"></canvas>
<img id="arenaTexture" src = "arenaTexture.jpg" hidden></img>
</body>
</html>
